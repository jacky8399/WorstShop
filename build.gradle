plugins {
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'java'
    id 'idea'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = 'https://jitpack.io' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'http://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven {
        name = 'minecraft-repo'
        url = 'https://libraries.minecraft.net/'
    }

    mavenLocal()
}

configurations.all {
    // Can't find where the Bukkit API was from lol
    exclude module: "bukkit"
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    // BungeeCord Chat
    compileOnly 'net.md-5:bungeecord-chat:1.16-R0.4'
    // Paper API
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.4-R0.1-SNAPSHOT'
    // compileOnly 'com.destroystokyo.paper:paper:1.16.4-R0.1-SNAPSHOT'
    // Mojang AuthLib
    compileOnly 'com.mojang:authlib:1.5.21'
    // LuckPerms
    compileOnly 'net.luckperms:api:5.0'
    // SmartInventory API
    compile(group: 'fr.minuskube.inv', name: 'smart-invs', version: '1.2.7') {
        transitive = false
    }
    // Economy
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
    // Commands
    compile 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    // PlayerPoints
    compileOnly files("libs/PlayerPoints.jar")
    // Placeholders
    compileOnly 'me.clip:placeholderapi:2.10.3'
    // Annotations
    compile 'org.jetbrains:annotations:16.0.2'
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    relocate 'fr.minuskube.inv', 'com.jacky8399.worstshop.inv'
    relocate 'co.aikar.commands', 'com.jacky8399.worstshop.acf'
    relocate 'co.aikar.locales', 'com.jacky8399.worstshop.acf.locales'
    //relocate 'co.aikar.minecraft-timings', 'com.jacky8399.worstshop.acf.minecraft-timings'
    relocate 'co.aikar.util', 'com.jacky8399.worstshop.acf.util'
    relocate 'net.jodah.expiringmap', 'com.jacky8399.worstshop.acf.expiringmap'
}

build.dependsOn shadowJar
