plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java-library'
    id 'idea'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        // Vault
        url = 'https://jitpack.io'
    }
    maven {
        // PlayerPoints
        url = 'https://repo.rosewooddev.io/repository/public/'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven {
        name = 'minecraft-repo'
        url = 'https://libraries.minecraft.net/'
    }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }

    mavenLocal()
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    // BungeeCord Chat
    compileOnly 'net.md-5:bungeecord-chat:1.16-R0.4'
    // Paper API
    compileOnly 'io.papermc.paper:paper-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper-server:1.18.1-R0.1-SNAPSHOT:mojang-mapped'
    // Mojang AuthLib
    compileOnly('com.mojang:authlib:1.5.21') {
        transitive = false
    }
    // LuckPerms
    compileOnly 'net.luckperms:api:5.3'
    // SmartInventory API
    api(group: 'fr.minuskube.inv', name: 'smart-invs', version: '1.2.7') {
        transitive = false
    }
    // Economy
    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        transitive = false
    }
    // Commands
    api 'co.aikar:acf-paper:0.5.1-SNAPSHOT'
    // PlayerPoints
    compileOnly('org.black_ixx:playerpoints:3.1.0') {
        transitive = false
    }
    // Placeholders
    compileOnly 'me.clip:placeholderapi:2.10.3'
    // QuickShop
    compileOnly('org.maxgamer:QuickShop:5.1.0.0-SNAPSHOT') {
        transitive = false
    }
    // PaperLib
    implementation "io.papermc:paperlib:1.0.7"
    // Annotations
    compileOnly 'org.jetbrains:annotations:22.0.0'
    // bStats
    implementation "org.bstats:bstats-bukkit:3.0.0"
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    relocate 'fr.minuskube.inv', 'com.jacky8399.worstshop.inv'
    relocate 'co.aikar.commands', 'com.jacky8399.worstshop.acf'
    relocate 'co.aikar.locales', 'com.jacky8399.worstshop.acf.locales'
    //relocate 'co.aikar.minecraft-timings', 'com.jacky8399.worstshop.acf.minecraft-timings'
    relocate 'co.aikar.util', 'com.jacky8399.worstshop.acf.util'
    relocate 'net.jodah.expiringmap', 'com.jacky8399.worstshop.acf.expiringmap'
    relocate "io.papermc.lib", "com.jacky8399.worstshop.paperlib"
    relocate "org.bstats", "com.jacky8399.worstshop.bstats"
}

build.dependsOn shadowJar
