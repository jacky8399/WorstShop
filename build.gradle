plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'java-library'
    id 'idea'
}

group = pluginGroup
version = pluginVersion

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven {
        name = 'papermc'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url = 'https://jitpack.io' }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven {
        name = 'minecraft-repo'
        url = 'https://libraries.minecraft.net/'
    }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }

    mavenLocal()
}

configurations {
    // wow! gradle magic!
    // makes test have the same sourceSet as main
    testCompile.extendsFrom compileOnly
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    // BungeeCord Chat
    compileOnly 'net.md-5:bungeecord-chat:1.16-R0.4'
    // Paper API
    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'io.papermc.paper:paper:1.17.1-R0.1-SNAPSHOT'
    // Mojang AuthLib
    compileOnly('com.mojang:authlib:1.5.21') {
        transitive = false
    }
    // LuckPerms
    compileOnly 'net.luckperms:api:5.3'
    // SmartInventory API
    api(group: 'fr.minuskube.inv', name: 'smart-invs', version: '1.2.7') {
        transitive = false
    }
    // Economy
    compileOnly("com.github.MilkBowl:VaultAPI:1.7") {
        transitive = false
    }
    // Commands
    api 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    // PlayerPoints (use jitpack.io)
    compileOnly('com.github.Mitsugaru:PlayerPoints:master-SNAPSHOT') {
        transitive = false
    }
    // Placeholders
    compileOnly 'me.clip:placeholderapi:2.10.3'
    // QuickShop
    compileOnly('org.maxgamer:QuickShop:4.0.8.0-SNAPSHOT') {
        transitive = false
    }
    // PaperLib
    implementation "io.papermc:paperlib:1.0.7"
    // Annotations
    compileOnly 'org.jetbrains:annotations:22.0.0'

    // Tests
    testImplementation "org.mockito:mockito-inline:3.6.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy = DuplicatesStrategy.WARN
        filter ReplaceTokens, tokens: [version: version]
    }
}

shadowJar {
    relocate 'fr.minuskube.inv', 'com.jacky8399.worstshop.inv'
    relocate 'co.aikar.commands', 'com.jacky8399.worstshop.acf'
    relocate 'co.aikar.locales', 'com.jacky8399.worstshop.acf.locales'
    //relocate 'co.aikar.minecraft-timings', 'com.jacky8399.worstshop.acf.minecraft-timings'
    relocate 'co.aikar.util', 'com.jacky8399.worstshop.acf.util'
    relocate 'net.jodah.expiringmap', 'com.jacky8399.worstshop.acf.expiringmap'
    relocate "io.papermc.lib", "com.jacky8399.worstshop.paperlib"
}

//test {
//    useJUnitPlatform()
//}

build.dependsOn shadowJar
